* DSP MERES - VASS GABOR ES TOTH BALINT - III.EVF. INFO, 1995
* IDEAL.ASM
* SIN X / X INTERPOLACIO  -  17 PONTOS

PRD	EQU 3		* BELSO ORA CIKLUSA
IMR	EQU 4		* IT MASZK
GREQ	EQU 5		* LOKALIS ES GLOBALIS MEMORIA HATARA

XN	EQU +0		* 6-OS LAP SEGEDVALTOZOI
XNM16	EQU +16
H0      EQU +20
H16     EQU +36
YN      EQU +40
MODE    EQU +41
ONE     EQU +42

	AORG 0
	B SETUP		* RESET HATASARA

	AORG >18
	B TINT		* BELSO TIMER IT RUTIN CIME

	AORG >20
CH0  DATA       0
CH1  DATA   -4214
CH2  DATA   -6953
CH3  DATA   -5900
CH4  DATA       0
CH5  DATA    9833
CH6  DATA   20860
CH7  DATA   29501
CH8  DATA   32767
CH9  DATA   29501
CH10  DATA   20860
CH11  DATA    9833
CH12  DATA       0
CH13  DATA   -5900
CH14  DATA   -6953
CH15  DATA   -4214
CH16  DATA       0
SETUP	EQU $
	DINT		* IT TILTAS
	LDPK 0		* 0-AS ADATLAP
	LALK >80
	SACL GREQ	* >80 LOKALIS, FELETTE GLOBALIS MEM.
	LACK >08
	SACL IMR	* BELSO ORA IT ENGEDELYEZES
	LALK +249
	SACL PRD	* BELSO ORA INIT (40KHZ ES KIMENET)
	LDPK >6		* 6-OS ADATLAP
        LACK 1
        SACL ONE
        LRLK AR0,>300+H16
        LARK AR1,16
        LACK CH16
        LARP AR0
LOAD    TBLR *-,AR1
        SUB ONE
        BANZ LOAD,*-,AR0
        LACK 1
        SACL MODE
	EINT		* IT ENGEDELYEZES

WAIT	IDLE		* VEGTELEN CIKLUS
	B WAIT

TINT	EQU $		* IT RUTIN: A FELDOLGOZAS
        LAC MODE
	SUBK 1
	SACL MODE
        BNZ NULLAK
	LACK 4
*        LACK +25
	SACL MODE
	IN XN,PA2	* A/D OLVASASA
*        LAC ONE,14
*	SACL XN
        OUT XN,PA0
        B TOVABB
NULLAK  LACK 0
        SACL XN
TOVABB  EQU $
*	OUT XN,PA0
        LRLK AR0,>300+XNM16
        LRLK AR1,>300+H16
	LRLK AR2,15
	ZAC
	LARP AR0
	LT *-,AR1
	MPY *-,AR0

LOOP	LTD *-,AR1
	MPY *-,AR2
        BANZ LOOP,*-,AR0

	APAC
	SACH YN,1
	OUT YN,PA2	* KIMENET IRASA
	OUT YN,PA7	* TRIGGER
	RET

	END
